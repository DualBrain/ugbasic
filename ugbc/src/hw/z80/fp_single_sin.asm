; z80float is a floating point library targeting the Z80 processor. While this
; project is tested on a TI-84+, the code should be generic enough to run on any
; Z80 CPU with very little modification (mostly defining scrap RAM locations).
;
; adapted by Marco Spedaletti

FPSINGLESIN:

	CALL FPPUSHPOP
	LD DE, FPSINGLE_CONST_PI_DIV_2
	CALL FPSINGLERSUB
	LD H, B
	LD L, C
	JP FPSINGLECOS_NOPUSHPOP

FPSINGLESIN_RANGE_REDUCED:

	LD DE, FPSINGLE_CONST_2PI
	CALL FPSINGLEMUL

	LD BC, FPSCRAP+4 ; FPSINGLECOS_Y
	LD D, H
	LD E, L

	CALL FPSINGLEMUL
	LD HL, FPSCRAP+7 ; FPSINGLECOS_Y
	SET 7, (HL)

	POP DE
	LD HL, FPSINGLESIN_A3
	LDI
	LDI
	LDI
	LD A, (HL)
	LD (DE), A
	LD B, D
	LD C, E
	DEC BC
	DEC BC
	DEC BC
	LD DE, FPSCRAP+4 ; FPSINGLECOS_Y
	LD HL, FPSINGLESIN_A2
	CALL FPSINGLEHORNER_STEP
	LD HL, FPSINGLESIN_A1
	CALL FPSINGLEHORNER_STEP
	LD HL, FPSINGLE_CONST_1
	CALL FPSINGLEHORNER_STEP
	LD HL, FPSCRAP ; FPSINGLECOS_X
	LD D, B
	LD E, C
	JP FPSINGLEMUL

FPSINGLESIN_A3:
  DEFB $11,$97,$4C,$39	;1.951123268E-4
FPSINGLESIN_A2:
  DEFB $AC,$83,$08,$3C	;.0083321742713
FPSINGLESIN_A1:
  DEFB $A4,$AA,$2A,$3E	;.1666665673
